unmanaged implementation in class ZJS_BP_TRAVEL_U_D unique;
strict ( 2 );
with draft;

define behavior for ZJS_I_TRAVEL_U_D alias Travel
implementation in class ZJS_BP_TRAVEL_U_D unique
draft table zjs_travel_d
etag master LastChangedAt
lock master total etag LastChangedAt
authorization master ( global )
late numbering
{
  field ( readonly ) TravelID, LastChangedAt;
  field ( mandatory ) AgencyID, CustomerID, BeginDate, EndDate;

  create;
  update;
  delete;

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;


  action ( features : instance ) set_status_booked result [1] $self;

  association _Booking { create ( features : instance ); with draft; }

  //determination test on save { create; field AgencyID; }

  mapping for zjs_travel control zjs_st_travel_intx
    {
      AgencyID      = agency_id;
      BeginDate     = begin_date;
      BookingFee    = booking_fee;
      CurrencyCode  = currency_code;
      CustomerID    = customer_id;
      EndDate       = end_date;
      LastChangedAt = lastchangedat;
      Memo          = description;
      Status        = status;
      TotalPrice    = total_price;
      TravelID      = travel_id;
    }
}


define behavior for ZJS_I_BOOKING_U_D alias Booking
implementation in class ZJS_BP_BOOKING_U_D unique
draft table zjs_booking_d
etag dependent by _Travel
lock dependent by _Travel
authorization dependent by _Travel
late numbering
{
  field ( readonly ) TravelID, BookingID;
  field ( mandatory ) BookingDate, CustomerID, AirlineID, ConnectionID, FlightDate;

  update;
  delete;

  association _BookSupplement { create; with draft; }
  association _Travel { with draft; }

  mapping for zjs_booking control zjs_st_booking_intx
    {
      AirlineID    = carrier_id;
      BookingDate  = booking_date;
      BookingID    = booking_id;
      ConnectionID = connection_id;
      CurrencyCode = currency_code;
      CustomerID   = customer_id;
      FlightDate   = flight_date;
      FlightPrice  = flight_price;
      TravelID     = travel_id;
    }
}


define behavior for ZJS_I_BOOKINGSUPPLEMENT_U_D alias BookingSupplement
implementation in class ZJS_BP_BOOKINGSUPPLEMENT_U_D unique
draft table zjs_book_suppl_d
etag dependent by _Travel
lock dependent by _Travel
authorization dependent by _Travel
late numbering
{
  field ( readonly ) TravelID, BookingID, BookingSupplementID;
  field ( mandatory : create, readonly : update ) SupplementID;
  field ( mandatory ) Price;

  update;
  delete;

  association _Travel { with draft; } //abbreviation Travel;
  association _Booking { with draft; }

  mapping for zjs_book_suppl control zjs_st_booking_supplement_intx
    {
      BookingID           = booking_id;
      BookingSupplementID = booking_supplement_id;
      CurrencyCode        = currency_code;
      Price               = price;
      SupplementID        = supplement_id;
      TravelID            = travel_id;
    }
}